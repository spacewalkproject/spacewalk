#!/usr/bin/perl
#
# Copyright (c) 2008--2010 Red Hat, Inc.
#
# This software is licensed to you under the GNU General Public License,
# version 2 (GPLv2). There is NO WARRANTY for this software, express or
# implied, including the implied warranties of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE. You should have received a copy of GPLv2
# along with this software; if not, see
# http://www.gnu.org/licenses/old-licenses/gpl-2.0.txt.
#
# Red Hat trademarks are not licensed under GPLv2. No permission is
# granted to use or replicate Red Hat trademarks that are incorporated
# in this software or its documentation.

use strict;
use warnings;

use English;

use Params::Validate qw(validate);
Params::Validate::validation_options(strip_leading => "-");

use Mail::RFC822::Address ();
use File::Spec ();
use DBI ();
use Digest::MD5 qw/md5_hex/;
use DateTime ();
use Sys::Hostname ();
use Spacewalk::Setup ();
use Fcntl qw(F_GETFD F_SETFD FD_CLOEXEC);

use RHN::DB ();

my $DEBUG;
$DEBUG = 0;

use constant DEFAULT_CA_CERT_NAME =>
  'RHN-ORG-TRUSTED-SSL-CERT';

use constant COBBLER_COMMAND => 'cobbler-setup';

my %opts = Spacewalk::Setup::parse_options();

my %answers = ();
Spacewalk::Setup::load_answer_file(\%opts, \%answers);
my $product_name = $answers{'product_name'} || 'Spacewalk';

# Skip the logfile init, normally just used when called from install.pl,
# which already did this.
if (not $opts{"skip-logfile-init"}) {
    Spacewalk::Setup::init_log_files($product_name, @ARGV);
}


if ($opts{"run-cobbler"}) {
    print Spacewalk::Setup::loc("* Setting up Cobbler..\n");
    setup_cobbler(\%opts, \%answers);
    exit 0;
}


$answers{hostname} ||= Sys::Hostname::hostname;

my %rhnOptions = ();
if (-e Spacewalk::Setup::DEFAULT_RHN_CONF_LOCATION) {
    Spacewalk::Setup::read_config(Spacewalk::Setup::DEFAULT_RHN_CONF_LOCATION,
        \%rhnOptions);
}

choose_database_schema(\%answers);

# If Oracle was selected, call the Oracle specific setup script. If it turns
# out we need a PostgreSQL specific setup script as well, refactor this to
# something generic.
if ($answers{'db-backend'} eq 'oracle') {
    Spacewalk::Setup::oracle_setup_db(\%opts, \%answers);
}
elsif ($answers{'db-backend'} eq 'postgresql') {
    Spacewalk::Setup::postgresql_setup_db(\%opts, \%answers);
}
else {
    die "No idea how to install to database: " . $answers{'db-backend'};
}

remove_old_jvm(\%opts, \%answers);

print Spacewalk::Setup::loc("* Setting up users and groups.\n");
setup_users_and_groups();

setup_services();
setup_gpg(\%opts);

setup_admin_email(\%opts, \%answers, \%rhnOptions);

print Spacewalk::Setup::loc("* Performing initial configuration.\n");
my $config_opts = populate_initial_configs(\%opts, \%answers);
mkdir_mount_points($config_opts->{'mount_point'},
	$config_opts->{'mount_point'} . '/redhat',
	$config_opts->{'kickstart_mount_point'});
setup_sudoers(\%opts, \%answers);

print Spacewalk::Setup::loc("* Activating $product_name.\n");
load_satellite_certificate(\%opts, \%answers);

print Spacewalk::Setup::loc("* Enabling Monitoring.\n");
setup_monitoring(\%opts, \%answers);

print Spacewalk::Setup::loc("* Configuring apache SSL virtual host.\n");
setup_ssl_vhost(\%opts, \%answers);

print Spacewalk::Setup::loc("* Configuring jabberd.\n");
setup_jabberd(\%opts, \%answers);

print Spacewalk::Setup::loc("* Creating SSL certificates.\n");
setup_ssl_certs(\%opts, \%answers);

print Spacewalk::Setup::loc("* Deploying configuration files.\n");
populate_final_configs(\%opts, \%answers);

print Spacewalk::Setup::loc("* Update configuration in database.\n");
final_db_config(\%opts, \%answers);

print Spacewalk::Setup::loc("* Setting up Cobbler..\n");
setup_cobbler(\%opts, \%answers);

print Spacewalk::Setup::loc("* Restarting services.\n");
Spacewalk::Setup::system_or_exit(['/usr/sbin/spacewalk-service', 'restart'], 40,
	       'Could not restart spacewalk services.');

wait_for_tomcat($answers{hostname});

Spacewalk::Setup::upgrade_stop_services(\%opts);
Spacewalk::Setup::oracle_upgrade_start_db(\%opts);

if ($opts{'upgrade'}) {
  print Spacewalk::Setup::loc("This portion of the $product_name upgrade process has successfully completed.\n");
  if ($product_name =~ /Satellite/) {
    print Spacewalk::Setup::loc("Please refer to appropriate upgrade document in /etc/sysconfig/rhn/satellite-upgrade\n");
    print Spacewalk::Setup::loc("for any remaining steps in the process.\n");
  }
} else {
  print Spacewalk::Setup::loc("Installation complete.\n");
  print Spacewalk::Setup::loc("Visit https://%s to create the $product_name administrator account.\n", $answers{hostname});
}

exit 0;



sub choose_database_schema {
    my $answers = shift;

    my %is_valid_schema = (
	'oracle' => 1,
	# Uncomment this to enable installation of postgresql schema:
	# NOTE: useful for development ONLY at this time.
	'postgresql' => 1,
	);

    my $question = "Choose your database backend ("
	. join(", ", sort(keys(%is_valid_schema)))
	. ")";

    Spacewalk::Setup::ask(
            -noninteractive => $opts{"non-interactive"},
            -question => $question,
            -test => sub {
                my $text = shift;
                $is_valid_schema{$text}
            },
            -answer => \$answers->{'db-backend'});

}

sub remove_old_jvm {
	my $opts = shift;
	my $answers = shift;

	return unless (($opts{"upgrade"}) and ($product_name =~ /Satellite/));

	my $jvm_list = Spacewalk::Setup::SHARED_DIR . "/old-jvm-list";

	local *F;
	open F, $jvm_list or die "Error opening [$jvm_list]: $!\n";
	my @jvms = <F>;
	close F;
	chomp @jvms;

	my $remove;

	foreach my $jvm (@jvms) {
		system("rpm -q $jvm >& /dev/null");
		$remove .= " $jvm" unless $? >> 8;
	}

	if ($remove) {
		print Spacewalk::Setup::loc("Setup found following old java packages:\n");
		foreach my $p (split (" ", $remove)) {
			print Spacewalk::Setup::loc("\t$p\n");
		}
	} else {
		return;
	}

	Spacewalk::Setup::ask(
		-noninteractive => $opts{"non-interactive"},
		-question => "Should setup remove these packages",
		-test => sub { my $text = shift; return $text =~ /^[YyNn]/ },
		-answer => \$answers->{"remove-old-jvm"},
                -default => 'Y',
	);
	unless ( $answers->{"remove-old-jvm"} =~ /^[Yy]/ ) {
		print Spacewalk::Setup::loc("** Skipping removal of old java packages.\n");
		return;
	}

	my $result = `rpm -e $remove 2>&1`;
	if ($? >> 8) {
		print Spacewalk::Setup::loc("** Error occured while removing the packages:\n");
		print Spacewalk::Setup::loc($result);
	}
}

sub setup_cobbler {
  my $opts = shift;
  my $answers = shift;
  if ($opts{'enable-tftp'}) {
    $answers{'enable-tftp'} = $opts{'enable-tftp'};
  }  
  if ($answers{'enable-tftp'} and $answers{'enable-tftp'} =~ /^[Yy]/ ) {
      system(COBBLER_COMMAND . " --enable-tftp=yes");
  }
  elsif ($answers{'enable-tftp'}) {
      system(COBBLER_COMMAND . " --enable-tftp=no");
  }
  elsif ($opts{"non-interactive"}) {
      system(COBBLER_COMMAND . " --enable-tftp=yes");
  }
  else {
      system(COBBLER_COMMAND);
  }  
}

sub setup_admin_email {
  my $opts = shift;
  my $answers = shift;
  my $rhnoptions = shift;

  if ($rhnoptions->{'traceback_mail'}) {
    $answers->{'admin-email'} = $rhnoptions->{'traceback_mail'};
  } else {
    Spacewalk::Setup::ask(
        -noninteractive => $opts{"non-interactive"},
        -question => "Admin Email Address",
        -test => sub { my $text = shift;
                       valid_multiple_email($text) && length($text) <= 128 },
        -answer => \$answers{'admin-email'});
  }
}


sub setup_sudoers {
	my $opts = shift;
	my $answers = shift;

	my ($sudoers_content_orig, $sudoers_content);
	my $sudoers_path = '/etc/sudoers';

	local *FILE;
	if (-e $sudoers_path) {
		# If /etc/sudoers exists, let's modify that
		open FILE, $sudoers_path or die "Error reading [$sudoers_path]: $!\n";
	} else {
		# otherwise start with our own file
		my $init_sudoers_path = Spacewalk::Setup::SHARED_DIR . '/sudoers.base';
		open FILE, $init_sudoers_path
			or die "Error reading [$init_sudoers_path]: $!\n";
		$sudoers_content_orig = '';
	}
	{
	local $/ = undef;
	$sudoers_content = <FILE>;
	$sudoers_content_orig = $sudoers_content if not defined $sudoers_content_orig;
	}
	close FILE;

	# The sudoers.clear file has regular expressions for clearing
	# old configuration
	if (open FILE, Spacewalk::Setup::SHARED_DIR . '/sudoers.clear') {
		while (defined(my $regexp = <FILE>)) {
			chomp $regexp;
			$sudoers_content =~ s!$regexp!!igm;
		}
		close FILE;
	}

	# The sudoers.1, ... files have parts of configuration that we
	# need to add to sudoers. The first line of each file helps us
	# detect that the part is already present, and potentially
	# modify it to the latest versions we need.
	for my $file (glob Spacewalk::Setup::SHARED_DIR . '/sudoers.[0-9]') {
		open FILE, $file or die "Error reading [$file]: $!\n";
		my $regexp = <FILE>;
		chomp $regexp;

		my $content;
		{
		local $/ = undef;
		$content = <FILE>;
		}
		close FILE;

		$sudoers_content =~ s!$regexp!$content!igm
			or $sudoers_content .= "\n" . $content;
	}

	if ($sudoers_content ne $sudoers_content_orig) {
		open FILE, "> $sudoers_path" or die "Error writing [$sudoers_path]: $!\n";
		chmod 0440, $sudoers_path;
		print FILE $sudoers_content;
		close FILE;
	}

	return;
}

sub passwords_match {
  my $password_1 = shift;
  my $password_2 = shift;

  if ($password_1 eq $password_2) {
    return 1;
  }

  print Spacewalk::Setup::loc("Passwords did not match, please try again.\n");

  return 0;
}

sub valid_ssl_cert_password {
  my $password = shift;

  my $ret;

  if (not $password) {
    print Spacewalk::Setup::loc("You must enter a password.\n");
    return 0;
  }

  if ($password =~ /([\t\r\n\f\013&+%\'\`\\\"=\#)])/) {
    $ret = $1;
  }

  if ($ret) {
    print Spacewalk::Setup::loc("Invalid character: '%s'.\n", $ret);
    return 0;
  }

  return 1;
}

sub valid_cert_countries {
  my $answers = shift;

  my $dbh = Spacewalk::Setup::get_dbh($answers);
  # NOTE: Removed a nvl on TL.short_name_tl and VC.short_name here,
  # short_name_tl was always NULL.
  my $sth = $dbh->prepare(<<EOQ);
SELECT  VC.code AS CODE,
          VC.short_name AS NAME
     FROM valid_countries_tl TL
		right join  valid_countries VC on  VC.code = TL.code and TL.lang = 'en'
ORDER BY VC.short_name
EOQ

  $sth->execute;

  my ($by_code, $by_name);

  while (my ($code, $name) = $sth->fetchrow) {
    $by_code->{$code} = $name;
    $by_name->{$name} = $code;
  }

  $sth->finish();
  $dbh->disconnect();

  return ($by_code, $by_name);
}

sub default_cert_expiration {
  my $dt = DateTime->now;
  my $dt2 = new DateTime (year => 2038, month => 1, day => 18);
  my $diff = $dt2 - $dt;

  return $diff->years - 1;
}


sub syscall_retry {
  my $localAnswer;
  Spacewalk::Setup::ask(
      -noninteractive => $opts{"non-interactive"},
      -question => "Setup can (t)ry again, (i)gnore the error and continue, or (q)uit running",
      -test => sub { my $text = shift;
               return $text =~ /^[TtIiQq]/ },
      -answer => \$localAnswer
  );
  if ( $localAnswer =~ /^[Ii]/ ) {
      Spacewalk::Setup::loc("Ignoring error and continuing\n");
      return 1;
  } elsif ( $localAnswer =~ /^[Qq]/ ) {
      die("Ending run at user's request");
  }
  return 0;
}


sub setup_ssl_vhost {
  my $opts = shift;
  my $answers = shift;

  if ($opts{"skip-ssl-vhost-setup"}) {
    print Spacewalk::Setup::loc("** Skipping SSL virtual host configuration.\n");
    return;
  }
  Spacewalk::Setup::ask(
    -noninteractive => $opts{"non-interactive"},
    -question => "Should setup configure apache's default ssl server for you (saves original ssl.conf)",
    -test => sub { my $text = shift; return $text =~ /^[YyNn]/ },
    -answer => \$answers->{"ssl-config-sslvhost"},
    -default => 'Y',
  );
  unless ( $answers->{"ssl-config-sslvhost"} =~ /^[Yy]/ ) {
    print Spacewalk::Setup::loc("** Skipping SSL virtual host configuration.\n");
    return;
  }

  my ($sslconf_content, $original_sslconf_content, $pre, $vhost, $post);
  my $sslconf_path = '/etc/httpd/conf.d/ssl.conf';
  local *FILE;

  die "$sslconf_path does not exist.\n" unless (-f $sslconf_path);

  {
    open FILE, $sslconf_path or die "Error opening [$sslconf_path]: $!\n";
    local $/ = undef;
    $sslconf_content = <FILE>;
    $original_sslconf_content = $sslconf_content;
    close FILE;
  }

  if ($sslconf_content =~ /(.*<VirtualHost _default_:443>)(.*)(<\/VirtualHost>.*)/s) {
    $pre = $1; $vhost = $2; $post = $3;
  } else {
    print Spacewalk::Setup::loc("Setup was unable to locate VirtualHost section in existing ssl configuration.\n");
    exit;
  }

  for my $file (glob Spacewalk::Setup::SHARED_DIR . '/ssl.conf.[0-9]') {
    open FILE, $file or die "Error reading [$file]: $!\n";
    my $regexp = <FILE>;
    chomp $regexp;

    my $content;
    {
	  local $/ = undef;
	  $content = <FILE>;
	}
	close FILE;
    $vhost =~ s!$regexp!$content!gm or $vhost .= $content;
  }

  $sslconf_content = $pre . $vhost . $post;

  if ($sslconf_content ne $original_sslconf_content) {
    my $done = 0;
    while (not $done) {
      system("cp", "--backup=numbered", $sslconf_path, "$sslconf_path-swsave");
      if ( $? >> 8 ) {
        print Spacewalk::Setup::loc("An error ocurred while attempting to back up your original ssl.conf\n");
        $done = syscall_retry;
      } else {
        print Spacewalk::Setup::loc("** /etc/httpd/conf.d/ssl.conf has been backed up to ssl.conf-swsave\n");
        $done = 1;
      }
    }
    open FILE, ">$sslconf_path" or die "Error opening [$sslconf_path]: $!\n";
    chmod 0644, $sslconf_path;
    print FILE $pre . $vhost . $post;
    close FILE;
  }

  return;
}

sub setup_jabberd {
  my $opts = shift;
  my $answers = shift;

  system('rpm -q jabberd >& /dev/null');
  if ($? >> 8 == 0) {
    system("/usr/bin/spacewalk-setup-jabberd", "--macros", "hostname:$answers->{'hostname'}");
  }
}

sub setup_ssl_certs {
  my $opts = shift;
  my $answers = shift;

  if ($opts{"skip-ssl-cert-generation"} || $opts{"upgrade"}) {
    print Spacewalk::Setup::loc("** Skipping SSL certificate generation.\n");
    return;
  }

  my ($password_1, $password_2);

  unless ($answers->{"ssl-password"}) {
    do {
      ($password_1, $password_2) = (undef, undef); # clear previous passwords
      Spacewalk::Setup::ask(
        -noninteractive => $opts{"non-interactive"},
        -question => "CA certificate password",
	  -test => \&valid_ssl_cert_password,
	  -answer => \$password_1,
	  -password => 1,
         );

      Spacewalk::Setup::ask(
        -noninteractive => $opts{"non-interactive"},
          -question => "Re-enter CA certificate password",
	  -test => \&valid_ssl_cert_password,
	  -answer => \$password_2,
	  -password => 1,
         );
    } until (passwords_match($password_1, $password_2));

    $answers->{"ssl-password"} ||= $password_1;
  };

  Spacewalk::Setup::ask(
      -noninteractive => $opts{"non-interactive"},
      -question => "Organization",
      -test => sub { my $text = shift;
		     return $text =~ /\S/ && length($text) <= 128 },
      -answer => \$answers->{"ssl-set-org"},
     );

  Spacewalk::Setup::ask(
      -noninteractive => $opts{"non-interactive"},
      -question => "Organization Unit",
      -test => sub { my $text = shift;
                     return $text =~ /\S/ && length($text) <= 128 },
      -default => $answers->{'hostname'},
      -answer => \$answers->{"ssl-set-org-unit"},
     );

  $answers->{"ssl-set-common-name"} ||= $answers->{hostname};

  Spacewalk::Setup::ask(
      -noninteractive => $opts{"non-interactive"},
      -question => 'Email Address',
      -test => sub { my $text = shift;
		     valid_multiple_email($text) && length($text) <= 128 },
      -default => $answers->{'admin-email'},
      -answer => \$answers->{'ssl-set-email'},
     );

  Spacewalk::Setup::ask(
      -noninteractive => $opts{"non-interactive"},
      -question => 'City',
      -test => sub { my $text = shift;
		     $text =~ /\S+/ && length($text) < 128 },
      -answer => \$answers->{'ssl-set-city'},
     );

  Spacewalk::Setup::ask(
      -noninteractive => $opts{"non-interactive"},
      -question => 'State',
      -test => sub { my $text = shift;
		     length($text) > 0 && length($text) < 128 },
      -answer => \$answers->{'ssl-set-state'},
     );

  my ($by_code, $by_name) = valid_cert_countries($answers);

  while (not $answers->{'ssl-set-country'}
	 or not (exists $by_code->{$answers->{'ssl-set-country'}}
		 or exists $by_name->{$answers->{'ssl-set-country'}})) {
    Spacewalk::Setup::ask(
        -noninteractive => $opts{"non-interactive"},
        -question => 'Country code (Examples: "US", "JP", "IN", or type "?" to see a list)',
	-test => sub { my $text = shift;
		       exists $by_code->{$text} or exists $by_name->{$text} or $text eq '?' },
	-answer => \$answers->{'ssl-set-country'},
       );

    if ($answers->{'ssl-set-country'} eq '?') {
      print_country_list($by_name);
      $answers->{'ssl-set-country'} = "";
    }
  }

  if (my $code = $by_name->{$answers->{'ssl-set-country'}}) {
    $answers->{'ssl-set-country'} = $code;
  }

  $answers->{'ssl-ca-cert-expiration'} ||= default_cert_expiration();
  $answers->{'ssl-server-cert-expiration'} ||= default_cert_expiration();

  my @hostname_parts = split(/\./, $answers->{hostname});
  my $system_name;

  if (scalar @hostname_parts > 2) {
    $system_name = join('.', splice(@hostname_parts, 0, -2));
  }
  else {
    $system_name = join('.', @hostname_parts);
  }

  $answers->{'ssl-server-rpm'} ||= 'rhn-org-httpd-ssl-key-pair-' . $system_name;
  $answers->{'ssl-dir'} ||= '/root/ssl-build';

  print Spacewalk::Setup::loc("** SSL: Generating CA certificate.\n");

  generate_ca_cert(-dir => $answers->{'ssl-dir'},
		   -password => $answers->{'ssl-password'},
		   '-set-country' => $answers->{'ssl-set-country'},
		   '-set-state' => $answers->{'ssl-set-state'},
		   '-set-city' => $answers->{'ssl-set-city'},
		   '-set-org' => $answers->{'ssl-set-org'},
		   '-set-org-unit' => $answers->{'ssl-set-org-unit'},
		   '-set-common-name' => $answers->{'ssl-set-common-name'},
		   '-cert-expiration' => $answers->{'ssl-ca-cert-expiration'},
		  );

  print Spacewalk::Setup::loc("** SSL: Deploying CA certificate.\n");

  deploy_ca_cert("-source-dir" => $answers->{'ssl-dir'},
		 "-target-dir" => '/var/www/html/pub');

  print Spacewalk::Setup::loc("** SSL: Generating server certificate.\n");

  generate_server_cert(-dir => $answers->{'ssl-dir'},
		       -password => $answers->{'ssl-password'},
		       '-set-country' => $answers->{'ssl-set-country'},
		       '-set-state' => $answers->{'ssl-set-state'},
		       '-set-city' => $answers->{'ssl-set-city'},
		       '-set-org' => $answers->{'ssl-set-org'},
		       '-set-org-unit' => $answers->{'ssl-set-org-unit'},
		       '-cert-expiration' => $answers->{'ssl-server-cert-expiration'},
		       '-set-email' => $answers->{'ssl-set-email'},
		       '-set-hostname' => $answers->{'hostname'},
		      );

  print Spacewalk::Setup::loc("** SSL: Storing SSL certificates.\n");

  install_server_cert(-dir => $answers->{'ssl-dir'},
		      -system => $system_name);

  Spacewalk::Setup::generate_server_pem(-ssl_dir => $answers->{'ssl-dir'},
		      -system => $system_name,
		      -out_file => '/etc/pki/spacewalk/jabberd/server.pem');

  store_ssl_cert(-ssl_dir => $answers->{'ssl-dir'});
}

sub print_country_list {
  my $by_name = shift;

  foreach my $name (sort keys %{$by_name}) {
    printf("%s\t%s\n", $by_name->{$name}, $name);
  }

  return
}
sub generate_ca_cert {
  my %params = validate(@_, {
			     dir => 1,
			     password => 1,
			     'set-country' => 1,
			     'set-state' => 1,
			     'set-city' => 1,
			     'set-org' => 1,
			     'set-org-unit' => 1,
			     'set-common-name' => 0,
			     'cert-expiration' => 1, # In years
			    });

  $params{'cert-expiration'} *= 365;

  my @opts = ( "--gen-ca", "--force" );

  foreach my $name (keys %params) {
    next unless ($params{$name});

    push @opts, qq(--$name=$params{$name});
  }

  Spacewalk::Setup::system_or_exit(['/usr/bin/rhn-ssl-tool', @opts], 35, 'Could not generate CA certificate.');

  return;
}

sub generate_server_cert {
  my %params = validate(@_, {
			     dir => 1,
			     password => 1,
			     'set-country' => 1,
			     'set-state' => 1,
			     'set-city' => 1,
			     'set-org' => 1,
			     'set-org-unit' => 1,
			     'cert-expiration' => 1,
			     'set-email' => 1,
			     'set-hostname' => 1,
			    });

  $params{'cert-expiration'} *= 365;

  my @opts = "--gen-server";

  foreach my $name (keys %params) {
    next unless ($params{$name});

    push @opts, qq(--$name=$params{$name});
  }

  Spacewalk::Setup::system_or_exit(['/usr/bin/rhn-ssl-tool', @opts], 36, 'Could not generate server certificate.');

  return;
}

sub deploy_ca_cert {
  my %params = validate(@_, { "source-dir" => 1,
			      "target-dir" => 1 });

  my @opts;

  foreach my $key (keys %params) {
    push @opts, qq(--$key=$params{$key});
  }

  Spacewalk::Setup::system_or_exit(['/usr/bin/rhn-deploy-ca-cert.pl', @opts], 37, 'Could not deploy CA certificate.');

  return;
}

sub install_server_cert {
  my %params = validate(@_, { dir => 1,
			      system => 1 });

  my @opts;

  push @opts, '--dir=' . File::Spec->catfile($params{dir}, $params{system});

  Spacewalk::Setup::system_or_exit(['/usr/bin/rhn-install-ssl-cert.pl', @opts], 38, 'Could not deploy SSL certificate.');

  return;
}

sub store_ssl_cert {
  my %params = validate(@_, { ssl_dir => 1,
			      ca_cert => { default => DEFAULT_CA_CERT_NAME },
			    });


  my $cert_path = File::Spec->catfile($params{ssl_dir}, $params{ca_cert});
  my @opts = ("--ca-cert=${cert_path}");

  Spacewalk::Setup::system_or_exit(['/usr/bin/rhn-ssl-dbstore', @opts], 39,
		 "There was a problem storing the SSL certificate.");

  return;
}

sub satellite_activate {
  my %params = validate(@_, { filename => 1,
			      sanity_only => 0,
			      disconnected => 0,
			    });

  my @args = ('--rhn-cert', $params{filename});

  if ($params{sanity_only}) {
    push @args, '--sanity-only';
  }

  if ($params{disconnected}) {
    push @args, '--disconnected';
  }

  # Only need to run the local checks the first time.
  if ($params{sanity_only}) {
    local_sat_cert_checks($params{filename});
  }

  my $ret = Spacewalk::Setup::system_debug('/usr/bin/rhn-satellite-activate',
		   @args);

  my %retcodes = (
		  10 => 'Could not parse certificate file',
		  11 => 'Certificate expired',
		  12 => "Unknown $product_name version",
		  20 => 'Remote activation failure',
		  30 => 'Local activation failure',
		  40 => 'Channel population failure',
		  80 => 'No management entitlements remaining',
		  82 => "$product_name channel not found",
		  83 => "No $product_name channel entitlements remaining",
		  84 => "Invalid $product_name certificate",
		  85 => 'Unknown activation error',
		  86 => "$product_name has no base channel on parent server",
		  87 => "No $product_name channel available for this version",
		  127 => 'Unhandled error',
		 );

  if ($ret) {
    my $exit_value = $? >> 8;

    die "(satellite_activation_failed) $retcodes{$exit_value}\n\n"
      if exists $retcodes{$exit_value};

    die "There was a problem validating the satellite certificate: $exit_value\n\n";
  }

  return 1;
}

sub local_sat_cert_checks {
  my $filename = shift;

  open(CERT, $filename) or die "Could not open '$filename': $OS_ERROR\n";
  my @data = <CERT>;
  close(CERT);

  my $cert_str = join('', @data);
  my ($signature, $cert);

  eval {
    ($signature, $cert) = Sat::Cert->parse_cert($cert_str);
  };
  if ($@) {
    die "(parse_error) Error parsing satellite cert: $@\n";
  }

  return 1;

  my $sat_version = 0; #PXT::Config->get('version');
  my $cert_version = $cert->get_field('satellite-version');

  #The cert version should be less specific than the sat version.
  my $match_length = length($cert_version);
  $sat_version = substr($sat_version, 0, $match_length);
  unless ($sat_version eq $cert_version) {
    die "(satellite_activation_failed) The version of the supplied cert ($cert_version)"
      . " did not match the version of this satellite ($sat_version)\n";
  }

  return;
}

sub load_satellite_certificate {
  my $opts = shift;
  my $answers = shift;

  if ($opts->{'upgrade'}) {
    print Spacewalk::Setup::loc("** Certificate not activated.\n");
    print Spacewalk::Setup::loc("** Upgrade process requires the certificate to be activated after the schema is upgraded.\n");
    return;
  }

  Spacewalk::Setup::ask(
      -noninteractive => $opts{"non-interactive"},
      -question => "Where is your satellite certificate file",
      -test => sub { my $text = shift; return (-e File::Spec->canonpath($text) and not -d File::Spec->canonpath($text)) },
      -answer => \$answers->{'satellite-cert-file'});

  print Spacewalk::Setup::loc("** Loading $product_name Certificate.\n");

  open(FH, $answers->{'satellite-cert-file'})
    or die "Could not open '" . $answers->{'satellite-cert-file'} . "' - $OS_ERROR.\n";

  my $cert_contents = do { local $INPUT_RECORD_SEPARATOR; <FH> };

  if (not $cert_contents) {
    print Spacewalk::Setup::loc("No data found in '%s'.\n", $answers->{'satellite-certificate-file'});
    exit 34;
  }

  my %activate_options;

  if ($opts->{disconnected}) {
    $activate_options{"-disconnected"} = 1;
  }

  eval {
    print Spacewalk::Setup::loc("** Verifying certificate locally.\n");

    satellite_activate(-filename => $answers->{'satellite-cert-file'},
		       -sanity_only => 1,
		      );

    print Spacewalk::Setup::loc("** Activating $product_name.\n");

    satellite_activate(-filename => $answers->{'satellite-cert-file'},
		       %activate_options);
  };

  if ($@) {
    my $E = $@;

    my $msg;

    if ($E =~ /\(satellite_activation_failed\) (.*)/) {
      $msg = $1;
      print Spacewalk::Setup::loc("There was a problem activating the satellite: %s.\n", $msg);
      exit 31;
    }
    elsif ($E =~ /\(parse_error\) (.*)/) {
      $msg = $1;
      print Spacewalk::Setup::loc($msg);
      exit 32;
    }
    else {
      $msg = $E;
      print Spacewalk::Setup::loc($msg);
      exit 33;
    }
  }

  return 1;
}

sub populate_initial_configs {
    my $opts = shift;
    my $answers = shift;

    # TODO: This may need to be addressed. Can query this for postgresql with 
    # "show client_encoding;":
    my $charset = 'UTF8';
    if ($answers{'db-backend'} eq 'oracle') {
        my %nls_database_paramaters = Spacewalk::Setup::get_nls_database_parameters($answers);
        $charset = $nls_database_paramaters{NLS_CHARACTERSET};
    }

    # Define some db specific settings:
    my ($hibernate_dialect, $hibernate_driver, $hibernate_url, $default_db);
    if ($answers->{'db-backend'} eq 'oracle') {
        $hibernate_dialect = "org.hibernate.dialect.Oracle9iDialect";
        $hibernate_driver = "oracle.jdbc.driver.OracleDriver";
        $hibernate_url = "jdbc:oracle:thin:@" . $answers->{"db-host"} . ":" . $answers->{"db-port"} . ":" . $answers->{"db-sid"};
        $default_db = sprintf('%s/%s@%s', @{$answers}{qw/db-user db-password db-sid/}),
    } elsif ($answers->{'db-backend'} eq 'postgresql') {
        $hibernate_dialect = "org.hibernate.dialect.PostgreSQLDialect";
        $hibernate_driver = "org.postgresql.Driver";
        $hibernate_url = "jdbc:postgresql://";
        $default_db = "dbname=$answers->{'db-name'}";
        if (defined $answers->{"db-host"} and $answers->{"db-host"} ne '') {
            $hibernate_url .= $answers->{"db-host"};
            $default_db .= ";host=$answers->{'db-host'}";
            if (defined $answers->{"db-port"} and $answers->{"db-port"} ne '') {
                $hibernate_url .= ':' . $answers->{"db-port"};
                $default_db .= ";port=$answers->{'db-port'}";
            }
        }
        $hibernate_url .= "/" . $answers->{"db-name"};
    }

  my %config_opts =
    (
     mount_point => $answers->{'mount-point'} || '/var/satellite',
     kickstart_mount_point => $answers->{'kickstart-mount-point'} || $answers->{'mount-point'} || '/var/satellite',
     serverDOTsatelliteDOThttp_proxy => $answers->{'rhn-http-proxy'} || '',
     serverDOTsatelliteDOThttp_proxy_username => $answers->{'rhn-http-proxy-username'} || '',
     serverDOTsatelliteDOThttp_proxy_password => $answers->{'rhn-http-proxy-password'} || '',
     webDOTis_monitoring_backend => 0,
     webDOTis_monitoring_scout => 0,
     webDOTenable_solaris_support => 0,
     osadispatcherDOTosa_ssl_cert => '/var/www/html/pub/RHN-ORG-TRUSTED-SSL-CERT',
     encrypted_passwords => 1,
     default_db => $default_db,
     db_backend => $answers->{'db-backend'},
     db_user => $answers->{'db-user'},
     db_password => $answers->{'db-password'},
     db_sid => $answers->{'db-sid'},
     db_host => $answers->{'db-host'},
     db_port => $answers->{'db-port'},
     hibernate_dialect => $hibernate_dialect,
     hibernate_driver => $hibernate_driver,
     hibernate_url => $hibernate_url,
     traceback_mail => $answers->{'admin-email'},
     jabberDOThostname => $answers->{hostname},
     osadispatcherDOTusername => 'rhn-dispatcher-sat',
     osadispatcherDOTpassword => 'rhn-dispatcher-' . random_password(6),
     serverDOTsatelliteDOTca_chain => '/usr/share/rhn/RHNS-CA-CERT',
     serverDOTnls_lang => 'english.' . $charset,
     server_secret_key => generate_secret(),
     webDOTssl_available => $opts->{'skip-ssl-cert-generation'} ? 0 : 1,
     monitoringDOTdbname => $answers->{'db-sid'},
     monitoringDOTusername => $answers->{'db-user'},
     monitoringDOTpassword => $answers->{'db-password'},
     monitoringDOTsmonDOTaddr => '127.0.0.1',
     monitoringDOTsmonDOTfqdn => 'localhost',
     monitoringDOTsmonDOTtestaddr => '127.0.0.1',
     monitoringDOTsmonDOTtestfqdn => 'localhost',
     monitoringDOTorahome => '/opt/oracle',
     monitoringDOTdbd => 'Oracle',
     monitoringDOTscout_shared_key => '', # blank for now.
     cobblerDOThost => $answers->{hostname}
    );

    for ($config_opts{'db_password'}) {
        s/\\/\\\\/g if defined $_;
    }

  my %rhnopt = ();
  if ($answers->{disconnected} || $opts->{disconnected}) {
    $rhnopt{'server.satellite.rhn_parent'} = '';
    $rhnopt{'disconnected'} = "1";
  }
  else {
    $rhnopt{'server.satellite.rhn_parent'} = $answers->{'rhn-parent'} || 'satellite.rhn.redhat.com';
  }
  for my $key (qw/product_name web.version enable_nvrea web.subscribe_proxy_channel force_package_upload
          web.l10n_resourcebundles web.default_mail_from/) {
    if (defined($answers->{$key})) {
      $rhnopt{$key} = $answers->{$key};
    }
  }
  write_config( \%rhnopt,
		'/etc/sysconfig/rhn-satellite-prep/etc/rhn/rhn.conf' );

    foreach my $opt_name (qw/session_swap_secret session_secret/) {
        foreach my $i (1 .. 4) {
            $config_opts{"${opt_name}_${i}"} = generate_secret();
        }
    }

  Spacewalk::Setup::generate_satcon_dict();
  write_config(\%config_opts, Spacewalk::Setup::DEFAULT_SATCON_DICT);

  Spacewalk::Setup::satcon_deploy();

    return \%config_opts;
}

# given a list of directories (by default, /var/satellite)
# check if they exist and if they do not exist, create them
# with apache as owner
sub mkdir_mount_points {
  system('/usr/bin/spacewalk-make-mount-points', @_);
}

sub populate_final_configs {
  my $options = shift;
  my $answers = shift;

  my $final_config = {satellite_install => 0,
		     };

  write_config($final_config, Spacewalk::Setup::DEFAULT_SATCON_DICT);

  Spacewalk::Setup::satcon_deploy(-tree => '/etc/sysconfig/rhn-satellite-prep/etc/rhn',
		-dest => '/etc/rhn');

  return;
}


sub final_db_config {
  my $options = shift;
  my $answers = shift;

  my $dbh = Spacewalk::Setup::get_dbh($answers);
  my $sth = $dbh->prepare(<<EOQ);
SELECT TS.value
  FROM rhnTemplateString TS
 WHERE TS.label = 'hostname'
EOQ

  $sth->execute();
  my ($current_hostname) = $sth->fetchrow();

  unless ($current_hostname) {
    $sth = $dbh->prepare(<<EOQ);
INSERT
  INTO rhnTemplateString
       (id, category_id, label, value, description)
VALUES (sequence_nextval('rhn_template_str_id_seq'),
        (SELECT TC.id FROM rhnTemplateCategory TC WHERE TC.label = 'org_strings'),
        'hostname',
        ?,
        'Host name for the RHN Satellite')
EOQ

    $sth->execute($answers->{hostname});

    if ($DEBUG) {
      $dbh->rollback();
    }
    else {
      $dbh->commit();
    }

  }

  $sth->finish;
  $dbh->disconnect();

  return;
}

sub write_config {
  my $options = shift;
  my $target = shift || Spacewalk::Setup::DEFAULT_RHN_CONF_LOCATION;

  my @opt_strings = map { "--option=${_}=" . $options->{$_} } grep { defined $options->{$_} } keys %{$options};

  Spacewalk::Setup::system_or_exit([ "/usr/bin/rhn-config-satellite.pl",
		   "--target=$target",
		   @opt_strings,
		 ],
		 29,
		 'There was a problem setting initial configuration.');

  return 1;
}

sub setup_monitoring {
  my $opts = shift;
  my $answers = shift;

  # write monitoring config to DB.
  my %mon_config =
    (
     RHN_ADMIN_EMAIL => $answers->{'admin-email'},
     MAIL_MX => $answers->{'mail-mx'} || 'localhost',
     MDOM => $answers->{mdom} || $answers->{hostname},
     RHN_DB_NAME => $answers->{'db-sid'},
     RHN_DB_USERNAME => $answers->{'db-user'},
     RHN_DB_PASSWD => $answers->{'db-password'},
     RHN_DB_TABLE_OWNER => $answers->{'db-user'},
     RHN_SAT_HOSTNAME => $answers->{hostname},
     XPROTO => 'https',
     RHN_SAT_WEB_PORT => 443
    );

  # DB connection params not yet written to rhn.conf, must specify them manually:
  my $connection_string =
    RHN::DB->get_dbi_connection_string($answers->{'db-backend'},
      $answers->{'db-host'}, $answers->{'db-port'}, $answers->{'db-sid'});
  RHN::DB->add_alias($connection_string, $answers->{'db-user'},
    $answers->{'db-password'}, { } );
  RHN::DB->set_default_connection($connection_string);

  update_monitoring_config($answers, \%mon_config);

  # Bugzilla 466747: On s390x, stty: standard input: Bad file descriptor
  # For some reason DBI mistakenly sets FD_CLOEXEC on a stdin file descriptor
  # here. This made it impossible for us to succesfully call `stty -echo`
  # later in the code. Following two lines work around the problem.

  my $flags = fcntl(STDIN, F_GETFD, 0);
  fcntl(STDIN, F_SETFD, $flags & ~FD_CLOEXEC);
  
  return 1;
}

sub update_monitoring_config {
  my $answers = shift;
  my $mon_config = shift;

  my $dbh = Spacewalk::Setup::get_dbh($answers);
  my $sth = $dbh->prepare(<<EOQ);
    UPDATE rhn_config_macro
    SET    definition = ?,
           last_update_user = 'installer',
           last_update_date = CURRENT_TIMESTAMP
    WHERE  name = ?
EOQ

  foreach my $name (keys %{$mon_config}) {
    $sth->execute($name, $mon_config->{$name});
  }

  if ($DEBUG) {
    $dbh->rollback();
  }
  else {
    $dbh->commit();
  }

  $dbh->disconnect();

  return;
}

sub generate_secret {
  return md5_hex(random_bits(4096));
}

sub random_bits {
  my $n = shift;

  open(RANDOM, '/dev/urandom') or die "could not open /dev/urandom for reading!\n";
  binmode(RANDOM);
  my $rand_data;
  my $result = read(RANDOM, $rand_data, $n >> 3);
  close(RANDOM);

  unless (defined $result) {
    die "could not read from /dev/urandom!\n";
  }

  return $rand_data;
}



sub random_password {
  my $length = shift;

  die "random password too short\n"
    unless $length > 5;

  my $ret;
  my @chars = ('a'..'z');
  my @nums = ('0'..'9');

  $ret .= $chars[int rand @chars]
    while $length-- > 5;

  $ret .= $nums[int rand @nums]
    while $length-- >= 0;

  return $ret;
}

sub setup_gpg {
  my $opts = shift;

  if ($opts->{"skip-gpg-key-import"}) {
    print Spacewalk::Setup::loc("** GPG: Skipping gpg key import\n");
    return 0;
  }

  print Spacewalk::Setup::loc("** GPG: Initializing GPG and importing key.\n");

  unless (-d '/root/.gnupg') {
    print Spacewalk::Setup::loc("** GPG: Creating /root/.gnupg directory\n");
    Spacewalk::Setup::system_or_exit(['mkdir', '-m', '700', '/root/.gnupg'], 12, 'Could not create /root/.gnupg');
  }

  Spacewalk::Setup::system_or_exit(['/usr/bin/gpg --list-keys'], 12, 'Could not run gpg.');

  my $key_path = '/usr/share/rhn/RHN-GPG-KEY';
  if ( ! (-e $key_path) ) {
    if ( -e '/etc/fedora-release' ) {

      # this is a fedora system
      $key_path = '/etc/pki/rpm-gpg/RPM-GPG-KEY-fedora';

    } elsif ( -e '/etc/redhat-release' ) {

      # need to read the file to see if it's a Red Hat or CentOS system.
      # we only want to import the key if it's a Red Hat system because
      # the file doesn't exist on a CentOS system.
      open(RELEASE, '/etc/redhat-release') or die "Could not open '/etc/redhat-release': $OS_ERROR\n";
      my @release = <RELEASE>;
      close(RELEASE);

      my $rel_str = join('', @release);

      # this is a RHEL system... RHEL 5 path.
      if ( $rel_str =~ m/^Red Hat/ ) {
        $key_path = '/etc/pki/rpm-gpg/RPM-GPG-KEY-redhat-release';
      }
    }
  }

  Spacewalk::Setup::system_or_exit(['/usr/bin/gpg', '--import', $key_path], 12, 'Could not import public GPG key.');

  return 1;
}

# Satellite services are handled by chkconfig now.
sub setup_services {
  Spacewalk::Setup::system_or_exit(["/usr/sbin/spacewalk-service", "--level", "345", "enable"], 11, 'Could not turn spacewalk services on.');

  return 1;
}

sub setup_users_and_groups {

    # Check to be sure the required users and groups exist.
    my @required_groups = qw/apache/;
    Spacewalk::Setup::check_groups_exist(@required_groups);

    # Need user tomcat in the apache group so the Java stack can read the same
    # configuration files as the rest of the application.
    (undef, undef, my $apache_group_id, my $apache_group_members) = getgrnam("apache");
    if (not defined $apache_group_id) {
        print Spacewalk::Setup::loc("The group 'apache' should exist.\n");
    }
    if (not grep { $_ eq 'tomcat' } split /\s+/, $apache_group_members) {
        Spacewalk::Setup::system_or_exit(['/usr/sbin/usermod', '-G', 'apache', '-a', 'tomcat'], 9,
            'Could not add tomcat to the apache group.');
    }

    return 1;
}

sub valid_multiple_email {
  my $text = shift || '';

  my @addys = grep { $_ } split(/[\s,]+/, $text);
  my $valid = 1;

  foreach my $addy (@addys) {
    if (not Mail::RFC822::Address::valid($text)) {
      print Spacewalk::Setup::loc("'%s' does not appear to be a valid email address.\n", $text);
      $valid = 0;
    }
  }

  unless (@addys) {
    print Spacewalk::Setup::loc("You must enter an email address.\n");

    $valid = 0;
  }

  return $valid;
}

sub wait_for_tomcat {
    my $hostname = shift;

    for (my $i = 0; $i < 20; $i++) {
        my $retval = system("HEAD http://$hostname/ 2>&1 > /dev/null");
        if ($retval) {
            sleep 5;
        }
        else {
            return;
        }
    }
    print "Tomcat failed to start properly or the installer ran out of tries.  Please check /var/log/tomcat*/catalina.out for errors.\n";
    return;
}

package Sat::Cert;

use XML::LibXML;
use IO::File;

sub new {
  my $class = shift;

  my $self = bless { }, $class;

  $self->_init();

  return $self;
}

sub parse_cert {
  my $class = shift;
  my $data = shift;

  $data =~ s/^\s+$//gm;

  my $p = new XML::LibXML;
  my $doc = $p->parse_string($data);
  my $root = $doc->getDocumentElement();

  die "invalid root\n" unless $root->getName eq 'rhn-cert';

  my ($signature_node) = $root->findnodes('rhn-cert-signature');
  my $signature = $signature_node ? $signature_node->getFirstChild->getData : undef;

  my @fields;
  my @extended_fields;

  foreach my $field_node ($root->findnodes('rhn-cert-field')) {
    my $name = $field_node->getAttribute('name');

    my @attributes = map { $_->getName } $field_node->getAttributes;

    # more than 1 means we had something besides name
    if (@attributes > 1) {
      push @extended_fields, [ $name, map { $_, $field_node->getAttribute($_) } grep { $_ ne 'name' } @attributes ];
    }
    else {
      if ($field_node->getFirstChild) {
	my $value = $field_node->getFirstChild->getData;

	push @fields, [ $name, $value ];
      }
      else {
	push @fields, [ $name, '' ];
      }
    }
  }

  my $cert = new $class;
  $cert->set_field($_->[0], $_->[1]) foreach @fields;

  $cert->push_field("name", @$_) foreach @extended_fields;

  return $signature, $cert;
}

sub add_field {
  my $self = shift;
  my @fields = shift;

  push @{$self->{fields}}, @fields;
}

sub set_field {
  my $self = shift;
  my $field = shift;
  my $val = shift;

  die "Field '$field' not allowed\n" unless grep { $_ eq $field } @{$self->{fields}};
  $self->{field_values}->{$field} = $val;
}

sub push_field {
  my $self = shift;
  my %vals = @_;

  my $field = delete $vals{name};
  die "no field in @_\n" unless $field;

  push @{$self->{field_values}->{$field}}, \%vals;
}

sub get_field {
  my $self = shift;
  my $field = shift;

  return $self->{field_values}->{$field};
}

sub set_required_fields {
  my $self = shift;
  my @fields = @_;

  $self->{required_fields} = \@fields;
}

sub _init {
  my $self = shift;

  my @fields = qw/product owner issued expires slots/;

  $self->add_field($_) foreach @fields;
  $self->set_required_fields(@fields);

  my @optional_fields = qw/virtualization_host virtualization_host_platform monitoring-slots provisioning-slots nonlinux-slots channel-families satellite-version generation/;
  $self->add_field($_) foreach @optional_fields;

  return;
}

sub version {
  my $self = shift;

  return $self->get_field('satellite-version');
}

__END__
